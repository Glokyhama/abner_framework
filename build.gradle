group 'abner'
version '1.0-SNAPSHOT'

// 应用Gradle各插件
apply plugin: 'java'

// war包生产插件
apply plugin: 'war'
apply plugin: 'idea'

// 项目版本
version = ""

// 指定编译编译.java文件的jdk版本
sourceCompatibility = 1.8

// 确保class文件与targetCompatibility指定版本，或者更新的java虚拟机兼容
targetCompatibility = 1.8

//
ext {

    // java虚拟机版本
    jvmVersion = "1.8"

    // spring的版本
    springVersion = "4.3.10.RELEASE"

    // 日志版本
    log4j2Version = "2.8.2"

    // 环境版本控制
    cpEnv = System.properties['profile'] == null ? "dev" : System.properties['profile'];
}

// 根据环境参数配置不同环境的参数
task initConfig(type: Copy) {

    from("src/main/resources/config-${cpEnv}") {
        include "*.properties"
        include "*.xml"
    }
    into "src/main/resources/config"
    exclude '**/*.bak'

    includeEmptyDirs = false
}

task initTest(type: Copy) {

    from("src/main/resources") {
        include "*.properties"
        include "*.xml"
    }
    into "src/test/resources"
    exclude '**/*.bak'

    includeEmptyDirs = false
}

compileJava.dependsOn(initConfig)

repositories {
    maven { url 'http://maven.springframework.org/release' }
    maven { url 'http://maven.springframework.org/milestone' }
    maven { url 'http://maven.springframework.org/snapshot' }
    maven { url 'http://download.java.net/maven/2' }
    mavenCentral()
    jcenter()
}

dependencies {

    // test
    testCompile "junit:junit:4.12"

    // spring
    compile(
            "org.springframework:spring-context:$springVersion",
            "org.springframework:spring-webmvc:$springVersion",
            "org.springframework:spring-tx:$springVersion",
            "org.springframework:spring-jdbc:$springVersion",
            "org.springframework:spring-test:$springVersion"
    )

    // mybatis
    compile(
            "org.mybatis:mybatis:3.4.4",
            "org.mybatis:mybatis-spring:1.3.1"
    )

    // db access
    compile "com.alibaba:druid:1.1.2"

    // json
    compile "com.alibaba:fastjson:1.2.9"

    // logging
    compile(

            "org.apache.logging.log4j:log4j-api:$log4j2Version",
            "org.apache.logging.log4j:log4j-core:$log4j2Version",
            "org.apache.logging.log4j:log4j-1.2-api:$log4j2Version",
            "org.apache.logging.log4j:log4j-web:$log4j2Version",
    )

    // mysql官方jdbc驱动程序
    compile "mysql:mysql-connector-java:5.1.43"

    // lmbok提供了简单的注解的形式来帮助我们简化消除一些必须有但显得很臃肿的java代码
    compile "org.projectlombok:lombok:1.16.18"

    // 常用字符处理工具类
    compile "org.apache.commons:commons-lang3:3.6"
}

// 编码
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'